-- Switch to pluggable database
ALTER SESSION SET CONTAINER = XEPDB1;
/

-- ========================
-- ORDERS Table
-- ========================
BEGIN
  EXECUTE IMMEDIATE '
    CREATE TABLE ORDERS (
      ORDER_ID NUMBER GENERATED BY DEFAULT AS IDENTITY PRIMARY KEY,
      CUSTOMER_ID NUMBER NOT NULL,
      ORDER_DATE DATE DEFAULT SYSDATE NOT NULL,
      STATUS VARCHAR2(20 CHAR) DEFAULT ''PENDING'' NOT NULL,
      TOTAL_AMOUNT NUMBER(10,2) NOT NULL,
      CURRENCY VARCHAR2(3 CHAR) DEFAULT ''USD'' NOT NULL,
      NOTES VARCHAR2(500 CHAR)
    )
  ';
EXCEPTION
  WHEN OTHERS THEN
    IF SQLCODE != -955 THEN  -- ignore "table already exists"
      RAISE;
    END IF;
END;
/

-- ========================
-- CHANGE_TRACKING Table
-- ========================
BEGIN
  EXECUTE IMMEDIATE '
    CREATE TABLE CHANGE_TRACKING (
      ID RAW(16) DEFAULT SYS_GUID() PRIMARY KEY,
      OFFSET_ID NUMBER GENERATED BY DEFAULT AS IDENTITY NOT NULL,
      TABLE_NAME VARCHAR2(100 CHAR) NOT NULL,
      TYPE VARCHAR2(20 CHAR) NOT NULL,
      DATA_CONTENT_TYPE VARCHAR2(100 CHAR),
      DATA CLOB CONSTRAINT CHK_JSON_DATA CHECK (DATA IS JSON),
      DATA_BASE64 CLOB,
      OFFSET_DATE_TIME TIMESTAMP WITH TIME ZONE DEFAULT CURRENT_TIMESTAMP NOT NULL
    )
  ';
EXCEPTION
  WHEN OTHERS THEN
    IF SQLCODE != -955 THEN
      RAISE;
    END IF;
END;
/

-- ========================
-- TRG_ORDERS_CHANGE_TRACKING Trigger
-- ========================
CREATE OR REPLACE TRIGGER TRG_ORDERS_CHANGE_TRACKING
AFTER INSERT OR UPDATE OR DELETE ON ORDERS
FOR EACH ROW
DECLARE
  v_action_type   VARCHAR2(10);
  v_json_data     CLOB;
  v_data_base64   CLOB;
BEGIN
  IF INSERTING THEN
    v_action_type := 'INSERT';
    v_json_data := TO_CLOB(JSON_OBJECT(
      'ORDER_ID' VALUE :NEW.ORDER_ID,
      'CUSTOMER_ID' VALUE :NEW.CUSTOMER_ID,
      'ORDER_DATE' VALUE TO_CHAR(:NEW.ORDER_DATE, 'YYYY-MM-DD"T"HH24:MI:SS'),
      'STATUS' VALUE :NEW.STATUS,
      'TOTAL_AMOUNT' VALUE TO_CHAR(:NEW.TOTAL_AMOUNT),
      'CURRENCY' VALUE :NEW.CURRENCY,
      'NOTES' VALUE :NEW.NOTES
    ));

  ELSIF UPDATING THEN
    v_action_type := 'UPDATE';
    v_json_data := TO_CLOB(JSON_OBJECT(
      'ORDER_ID' VALUE :NEW.ORDER_ID,
      'CUSTOMER_ID' VALUE :NEW.CUSTOMER_ID,
      'ORDER_DATE' VALUE TO_CHAR(:NEW.ORDER_DATE, 'YYYY-MM-DD"T"HH24:MI:SS'),
      'STATUS' VALUE :NEW.STATUS,
      'TOTAL_AMOUNT' VALUE TO_CHAR(:NEW.TOTAL_AMOUNT),
      'CURRENCY' VALUE :NEW.CURRENCY,
      'NOTES' VALUE :NEW.NOTES
    ));

  ELSIF DELETING THEN
    v_action_type := 'DELETE';
    v_json_data := TO_CLOB(JSON_OBJECT(
      'ORDER_ID' VALUE :OLD.ORDER_ID,
      'CUSTOMER_ID' VALUE :OLD.CUSTOMER_ID,
      'ORDER_DATE' VALUE TO_CHAR(:OLD.ORDER_DATE, 'YYYY-MM-DD"T"HH24:MI:SS'),
      'STATUS' VALUE :OLD.STATUS,
      'TOTAL_AMOUNT' VALUE TO_CHAR(:OLD.TOTAL_AMOUNT),
      'CURRENCY' VALUE :OLD.CURRENCY,
      'NOTES' VALUE :OLD.NOTES
    ));
  END IF;

  SELECT UTL_RAW.CAST_TO_VARCHAR2(
           UTL_ENCODE.BASE64_ENCODE(UTL_RAW.CAST_TO_RAW(v_json_data))
         )
  INTO v_data_base64
  FROM DUAL;

  INSERT INTO CHANGE_TRACKING (
    TABLE_NAME,
    TYPE,
    DATA_CONTENT_TYPE,
    DATA,
    DATA_BASE64
  ) VALUES (
    'ORDERS',
    v_action_type,
    'application/json',
    v_json_data,
    v_data_base64
  );
END;
/
